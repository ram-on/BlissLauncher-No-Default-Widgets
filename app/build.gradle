import foundation.e.blisslauncher.buildsrc.Libs
import foundation.e.blisslauncher.buildsrc.Versions

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: '../spotless/spotless.gradle'

// Manifest version information!
def versionMajor = 1
def versionMinor = 3
def versionPatch = 2

android {
    compileSdkVersion Versions.compile_sdk
    defaultConfig {
        applicationId "foundation.e.blisslauncher"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
        }

        applicationVariants.all { variant ->
            if (variant.buildType.name == "debug") {
                variant.outputs.all { output ->
                    outputFileName = "BlissLauncher-${variant.getFlavorName()}-${variant.versionName}.apk"
                }
            }

        }
    }

    signingConfigs {
        debug {
            storeFile file(getRootDir().path + "/keystore/platform.keystore")
            storePassword 'android'
            keyAlias 'platform'
            keyPassword 'android'
        }
    }

    flavorDimensions "api"
    productFlavors {
        apiNougat {
            dimension "api"
            minSdkVersion 24
            compileSdkVersion 25
        }
        apiOreo {
            dimension "api"
            minSdkVersion 26
            compileSdkVersion 28
            targetSdkVersion 28
        }
        apiQ {
            dimension "api"
            minSdkVersion 29
            compileSdkVersion 29
            targetSdkVersion 29
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java.srcDirs += ['src/quickstep/src', 'src/quickstep/recents_ui_overrides/src']
            res.srcDirs += ['src/quickstep/res', 'src/quickstep/recents_ui_overrides/res']
        }
        apiQ {
            java.srcDirs = ['src/apiOreo/java/']
            res.srcDirs = ['src/apiOreo/res']
            manifest.srcFile "src/apiOreo/AndroidManifest.xml"
        }
    }
    addFrameworkJar('framework.jar')
}

dependencies {
    implementation 'me.relex:circleindicator:2.1.4'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    //compileOnly files('libs/framework.jar')
    implementation project(':SystemUISharedLib')
    apiNougatImplementation 'org.cyanogenmod:platform.sdk:6.0'
    apiOreoImplementation files('libs/lineage-sdk-oreo.jar')
    apiQImplementation files('libs/lineage-sdk-q.jar')
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

    implementation 'org.greenrobot:eventbus:3.1.1'

    // Recents dependency


    // Support Libs
    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.recyclerview

    // Rx Java and Android
    implementation Libs.RxJava.rxAndroid
    implementation Libs.RxJava.rxJava
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0'

    // Retrofit
    implementation Libs.Retrofit.retrofit
    implementation Libs.Retrofit.gsonConverter
    implementation Libs.Retrofit.retrofit_rxjava_adapter

    // Okhttp
    implementation Libs.OkHttp.okhttp
    implementation Libs.OkHttp.loggingInterceptor

    // Rx Relay
    implementation "com.jakewharton.rxrelay2:rxrelay:2.1.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Blur Library
    implementation 'com.hoko:hoko-blur:1.3.4'

    // Room
    implementation Libs.AndroidX.Room.runtime
    kapt Libs.AndroidX.Room.compiler

    // Unit test dependencies
    testImplementation Libs.junit
    testImplementation Libs.robolectric
    testImplementation "org.mockito:mockito-core:2.19.0"
    testImplementation Libs.AndroidX.Test.archCoreTesting

    // Instrumentation tests dependencies
    androidTestImplementation Libs.junit
    androidTestImplementation Libs.AndroidX.Test.espressoCore
    androidTestImplementation Libs.AndroidX.Test.runner
    androidTestImplementation Libs.AndroidX.Test.rules
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
}
